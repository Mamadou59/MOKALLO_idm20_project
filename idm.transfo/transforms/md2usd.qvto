
modeltype mdMM "strict" uses "http://www.example.org/simplemarkdown";
modeltype usdMM "strict" uses "http://www.example.org/usd";
transformation md2usd(in md : mdMM, out usd : usdMM); 

main() {
	//log("Test","j'entre'");
	md.rootObjects()[Document] -> map document2Page()
}

mapping mdMM::Document::document2Page() :usdMM::Page  {
	init {
		//log("Test",self.name);
	}
	name := self.name;
	contents += self.contents -> map constructContent();		
}

// Start transformation block to pagecontent
abstract mapping mdMM::Block::block2PageContent() : usdMM::PageContent {
	
}

// Start transformation TextContainer md to TextContainer usd
abstract mapping mdMM::TextContainer::textContainerMd2TextContainerUsd() : usdMM::TextContainer inherits mdMM::Block::block2PageContent {
	textElements += self.elements -> map contructTextElement();
}


mapping mdMM::Paragraph::paragraphMd2ParagraphUsd() : usdMM::Paragraph inherits mdMM::TextContainer::textContainerMd2TextContainerUsd{
	
}

mapping mdMM::Title::titleMd2TitleUsd() : usdMM::Title inherits mdMM::TextContainer::textContainerMd2TextContainerUsd{
	level := self.level;
}

// Strat TextElement part
abstract mapping mdMM::TextElement::textElementMd2TextElementUsd() : usdMM::TextElement {
	text := self.text;
}

mapping mdMM::Text::textMd2TextUsd() : usdMM::Text inherits mdMM::TextElement::textElementMd2TextElementUsd{
	
}

mapping mdMM::StrongEmphasis::strongEmphasisMd2StrongEmphasisUsd() : usdMM::StrongEmphasis inherits mdMM::TextElement::textElementMd2TextElementUsd{
	
}

// TODO review the mdMM correspondance
mapping mdMM::Emphasis::emphasisMd2ItalicEmphasisUsd() : usdMM::ItalicEmphasis inherits mdMM::TextElement::textElementMd2TextElementUsd{
	
}

mapping mdMM::Code::codeMd2CodeUsd() : usdMM::Code inherits mdMM::TextElement::textElementMd2TextElementUsd {

}

// Start LinkTextElement transformation
abstract mapping mdMM::LinkTextElement::linkTextElementMd2LinkTextElementUsd() : usdMM::LinkTextElement inherits mdMM::TextElement::textElementMd2TextElementUsd{
	url := self.url;
	title := self.title;
	textElements += self.textElements -> map constructLinkTextElement();
}

mapping mdMM::Link::linkMd2LinkUsd() : usdMM::Link inherits mdMM::LinkTextElement::linkTextElementMd2LinkTextElementUsd {

}

mapping mdMM::Image::imageMd2ImageUsd() : usdMM::Image inherits mdMM::LinkTextElement::linkTextElementMd2LinkTextElementUsd {

}
mapping mdMM::TextElement::constructLinkTextElement() : usdMM::TextElement  
	disjuncts mdMM::Link::linkMd2LinkUsd, mdMM::Image::imageMd2ImageUsd {}
	
// End LinkTextElement transformation

mapping mdMM::TextElement::contructTextElement() : usdMM::TextElement 
	disjuncts  mdMM::Text::textMd2TextUsd, mdMM::StrongEmphasis::strongEmphasisMd2StrongEmphasisUsd,
				mdMM::Emphasis::emphasisMd2ItalicEmphasisUsd, mdMM::Code::codeMd2CodeUsd,
				mdMM::Link::linkMd2LinkUsd, mdMM::Image::imageMd2ImageUsd{}
	
//End TextElement part


// Start List transformation
//abstract mapping mdMM::List::ListMd2ListUsd() : ListElement inherits mdMM::Block::block2PageContent {
//	
//}

mapping mdMM::OrderedList::orderedListMd2OrderedListUsd() : usdMM::OrderedList  {
	items += self.items -> map listItemMd2ListItemUsd();
}

mapping mdMM::BulletList::unorderedListMd2UnorderedListUsd() : usdMM::UnorderedList {
	items += self.items -> map listItemMd2ListItemUsd();
}

mapping mdMM::ListItem::listItemMd2ListItemUsd() : usdMM::ListItem {
	elements += self.elements -> map constructListItemElement();
}

//abstract mapping mdMM::ListItemElement::listItemElementMd2ListItemElementUsd() : usdMM::ListItemElement {
//	
//}

//mapping mdMM::OrderedListItem::listItemMd2ListItemUsd() : usdMM::ListItem {
//}
//mapping mdMM::BulletListItem::listItemMd2ListItemUsd() : usdMM::ListItem {
//}

mapping mdMM::ListItemElement::constructListItemElement() : usdMM::ListItemElement 
	disjuncts mdMM::OrderedList::orderedListMd2OrderedListUsd, mdMM::BulletList::unorderedListMd2UnorderedListUsd,
	 			mdMM::Paragraph::paragraphMd2ParagraphUsd{
	
}
// End List transformation

mapping mdMM::Block::constructContent() : usdMM::PageContent 
	disjuncts  mdMM::Paragraph::paragraphMd2ParagraphUsd, mdMM::Title::titleMd2TitleUsd,
	mdMM::OrderedList::orderedListMd2OrderedListUsd,
	mdMM::BulletList::unorderedListMd2UnorderedListUsd{}
// End transformation TextContainer md to TextContainer usd

// End transformation block to pagecontent

