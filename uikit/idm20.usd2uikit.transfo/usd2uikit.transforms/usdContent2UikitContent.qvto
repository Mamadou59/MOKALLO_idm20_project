/**
* This module ensures usd transformation to UIkit of all element contained directly in PageContent.
*/

import usdTextElement2UikitTextElement;
	
modeltype usdMM "strict" uses "http://www.example.org/usd";
modeltype uikitMM "strict" uses "http://www.example.org/uikit";

transformation usd2bstrap(in usd : usdMM, out uikit : uikitMM);

// =========== Start transformation PageContent to Pagecontent ===========//

/**
* Abstract mapper to map an usd PageContent element to Uikit PageContent element.
*/
abstract mapping usdMM::PageContent::pageContentUsd2PageContentUikit() : uikitMM::PageContent {}

// =========== Start transformation TextContainer to TextContainer ===========//

/**
* Abstract mapper to map an usd TextContainer element to Uikit TextContainer element.
*/
abstract mapping usdMM::TextContainer::textContainerUsd2TextContainerUikit() : uikitMM::TextContainer {
	textelements += self.textElements -> map constructTextElement();
	var textWithProps := self.textElements[usdMM::Text] -> selectOne(t | t.text.isPropertyLine());
	if (textWithProps <> null) {
		color := textWithProps.text.findColor();
		var props := textWithProps.text.substringAfter("properties");
		
		if (props.find('background:') > 0){
				generalproperties += props.backGroundWrapper();
		};
		if (props.find('margin:') > 0){
			generalproperties += props.marginWrapper();
		};
		if (props.find('padding:') > 0){
			generalproperties += props.paddingWrapper();
		};
	}
}

// =========== Start transformation Title to Title ===========//

/**
* A mapper which map an usd Title element to Uikit Title element.
*/
mapping usdMM::Title::titleUsd2TitleUikit() : uikitMM::Title 
	inherits usdMM::TextContainer::textContainerUsd2TextContainerUikit{
	level := self.level;
	var textWithProps := self.textElements[usdMM::Text] -> selectOne(t | t.text.isPropertyLine());
	if (textWithProps <> null) {
		var props := textWithProps.text.substringAfter("properties");
		if (props.find("titleSize") > 0) {
			titleproperties += object uikitMM::TitleProperty {
				size := ("size:"+(props.substringAfter("titleSize:").findProperty())).getSize();
			}
		}
	}
	
}

// =========== Start transformation Paragraph to Pargraph ===========//
/**
* A mapper which map an usd Paragraph element to uikit Paragraph element.
*/
mapping usdMM::Paragraph::paragraphUsd2ParagraphUikit() : uikitMM::Paragraphe 
	inherits usdMM::TextContainer::textContainerUsd2TextContainerUikit{}

// =========== Start transformation Section to Container ===========//

/**
* A mapper which map an usd Section element to Uikit Container element.
*/
mapping usdMM::Section::sectionUsd2ContainerUikit() : uikitMM::Container {
	contents += self.contents -> map constructPageContent();
	var textWithProps := self.contents[usdMM::Paragraph].textElements[usdMM::Text] -> 
							selectOne(t | t.text.find("properties:") > 0);
	if (textWithProps <> null) {
		var props := textWithProps.text.substringAfter("properties:");
		
		if (props.find('background:') > 0){
				generalproperties += props.backGroundWrapper();
		};
		if (props.find('margin:') > 0){
			generalproperties += props.marginWrapper();
		};
		if (props.find('padding:') > 0){
			generalproperties += props.paddingWrapper();
		};
	}
}

// =========== This mapper make disjunction beetwen all PageContent elements mappers ===========//

/**
* Map a usd PageContent element to bstrap PageContent element, 
* wich make disjunction beetwen these maps paragraphUsd2ParagraphUikit,
* titleUsd2TitleUikit and sectionUsd2ContainerUikit
*/
mapping usdMM::PageContent::constructPageContent() : uikitMM::PageContent 
	disjuncts usdMM::Title::titleUsd2TitleUikit, usdMM::Paragraph::paragraphUsd2ParagraphUikit,
				usdMM::Section::sectionUsd2ContainerUikit{}
				
				